# vi: set ft=fish

type -qa git
if test $status != 0
  set_color $fish_color_error
  echo 'ERROR: `git` command not found'
  set_color normal
  exit 1
end

# NOTE: Install fresco
set -l fresco_fish_path "$HOME/.config/fish/conf.d/fresco.fish"
if set -q XDG_CONFIG_HOME
    set fresco_fish_path "$XDG_CONFIG_HOME/fish/conf.d/fresco.fish"
end
if not set -q fresco_root
    set -U fresco_root "$HOME/.local/share/fresco/repos"
    if set -q XDG_DATA_HOME
        set fresco_root "$XDG_DATA_HOME/fresco/repos"
    end
end

set -l url github.com/masa0x80/fresco
set -l fresco_base_path $fresco_root/$url
mkdir -p $fresco_base_path
git clone https://$url $fresco_base_path

set_color $fish_color_user
echo "Create $fresco_fish_path"
mkdir -p (dirname $fresco_fish_path)
set_color normal

# NOTE: Create bootstrap file
echo "set -l bootstrap_file $fresco_base_path/fresco.fish"                 > $fresco_fish_path
echo 'if test -r $bootstrap_file'                                         >> $fresco_fish_path
echo '    source $bootstrap_file'                                         >> $fresco_fish_path
echo 'else'                                                               >> $fresco_fish_path
echo "    mkdir -p $fresco_base_path"                                     >> $fresco_fish_path
echo "    git clone https://github.com/masa0x80/fresco $fresco_base_path" >> $fresco_fish_path
echo 'end'                                                                >> $fresco_fish_path

source $fresco_fish_path
